plugins {
	id 'net.minecrell.licenser' version '0.3'
	
	id 'java'
	id 'eclipse'
	id 'maven'
}

license {
	header = project.file('HEADER')
}

group = 'com.unascribed'
archivesBaseName = 'mavkit'
version = '0.0.1'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

ext.targetJavaVersion = JavaVersion.VERSION_1_8

repositories {
	mavenCentral()
	mavenLocal()
	maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
	ivy { artifactPattern "http://github.com/[organization]/raw/[revision]/[module].[ext]" }
}

configurations {
	checkerFrameworkAnnotatedJDK
	checkerFramework
}

project.ext.lwjglVersion = "3.0.0"

dependencies {
	compile "org.lwjgl:lwjgl:${lwjglVersion}"
	compile "org.lwjgl:lwjgl-platform:${lwjglVersion}:natives-linux"
	compile "org.lwjgl:lwjgl-platform:${lwjglVersion}:natives-osx"
	compile "org.lwjgl:lwjgl-platform:${lwjglVersion}:natives-windows"
	
	
	compile 'org.slf4j:slf4j-simple:1.7.13'
	compile 'com.google.guava:guava:19.0'
	// we want both of these so that Checker Framework can find Guava's annotations
	compile "org.checkerframework:checker-qual:2.1.4"
	compile 'com.google.code.findbugs:jsr305:3.0.1'
	
	compile 'org.ow2.asm:asm:5.1'
	compile 'net.sf.jopt-simple:jopt-simple:5.0.3'
	compile 'net.java.dev.jna:jna:4.2.2'
	compile 'net.java.dev.jna:jna-platform:4.2.2'
	
	checkerFrameworkAnnotatedJDK "org.checkerframework:jdk8:2.1.4"

	checkerFramework "org.checkerframework:checker:2.1.4"
}

tasks.withType(JavaCompile).all { JavaCompile compile ->
	compile.options.compilerArgs = [
		'-processor', 'org.checkerframework.checker.nullness.NullnessChecker,'+
		              'org.checkerframework.checker.tainting.TaintingChecker,'+
		              'org.checkerframework.checker.regex.RegexChecker,'+
		              'org.checkerframework.checker.propkey.PropertyKeyChecker,'+
		              'org.checkerframework.checker.i18n.I18nChecker,'+
		              //'org.checkerframework.checker.guieffect.GuiEffectChecker,'+
		              'org.checkerframework.checker.signedness.SignednessChecker',
		'-Apropfiles=src/main/resources/com/unascribed/mavkit/lang.properties',
		'-processorpath', "${configurations.checkerFramework.asPath}",
		"-Xbootclasspath/p:${configurations.checkerFrameworkAnnotatedJDK.asPath}"
	]
}

task wrapper(type: Wrapper) {
	gradleVersion = '3.1'
}
